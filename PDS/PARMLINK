*          PARM DRIVER         AT LEVEL 1.2 AS OF 12/30/01
   TITLE 'PARMLINK -- DRIVER FOR PARMCHEK PROGRAM'
**************************************************************
**************************************************************
**                                                          **
**                                                          **
**  MODULE:   PARMLINK                                      **
**                                                          **
**  AUTHOR:   SAM GOLOB                                     **
**                                                          **
**  PURPOSE:  DRIVER FOR PARMCHEK PROGRAM                   **
**                                                          **
**                                                          **
**************************************************************
**************************************************************
**                                                          **
**  CHANGE LOG:                                             **
**                                                          **
**************************************************************
**************************************************************
         EJECT
**************************************************************
*  ---------------------------------------------------------------  *
*    This macro is used to display hex values in a printed report.
*  ---------------------------------------------------------------  *
         MACRO
&NAME    HEX   &TO,&LEN,&FROM
&NAME    DS    0H
         ST    R4,SAV4HEX
         STM   15,1,HEXSAVE
         LA    1,&FROM
         LA    0,&LEN
         LA    15,&TO
         BAL   4,HEX
         L     R4,SAV4HEX
         LM    15,1,HEXSAVE
         MEND
         SPACE
**************************************************************
         GBLC  &LLEV
&LLEV    SETC  '1.2 '
         YREGS
PARMLINK CSECT
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'PARMLINK - DRIVER FOR PARM CHECK PRGM  '
         DC    C' - '
         DC    C'LEVEL '
SYSLEV   DC    C'&LLEV'
         DC    C' - '
SYSDAT   DC    C'&SYSDATE'
         DC    C'  ',C'&SYSTIME ',C'--- '
         DC    C'Copyright (c) 2001 by Sam Golob '
         DC    C'--- '            MAKE EYECATCHER VISIBLE
SAVEAREA DC    18F'0'
EYECATCH DS    0D             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          Save PARM Pointer
         ST    R2,VALUSAVE    Save Values Pointer
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP BASE REGISTERS
         USING PARMLINK,R12,R11,R10
         LR    R12,R15
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LA    R10,4095(,R11)
         LA    R10,1(,R10)
         LR    R1,R5          RESTORE PARM POINTER
         ST    R1,PARMSAVE
* ----------------------------------------------------------------- *
***********************************************************************
* GET SYSTEM DATE AND TIME - A standard way to get date and time      *
***********************************************************************
         STM   R14,R2,SAVE1402
         TIME  DEC,DATETIME,LINKAGE=SYSTEM,DATETYPE=MMDDYYYY
         OI    DATE+4,X'0F'
         UNPK  DATED,DATE
         OI    TIME+3,X'0F'
         UNPK  TIMED,TIME
*                          FILL HEADDATE AREA WITH INFO TO REUSE
         MVC   HEADDATE(2),DATED
         MVI   HEADDATE+2,C'/'
         MVC   HEADDATE+3(2),DATED+2
         MVI   HEADDATE+5,C'/'
         MVC   HEADDATE+6(4),DATED+4
         MVC   HEADDATE+12(2),TIMED
         MVI   HEADDATE+14,C':'
         MVC   HEADDATE+15(2),TIMED+2
         MVI   HEADDATE+17,C':'
         MVC   HEADDATE+18(2),TIMED+4
         MVC   MESSAGE1+98(31),HEADDLIT    TODAY'S DATE AND TIME
         LM    R14,R2,SAVE1402
***********************************************************************
         MVC   LEVDATE(8),SYSDAT
***********************************************************************
         MVC   MESSAGE1+71(21),LEVELLIT    PROGRAM LEVEL AND
* ----------------------------------------------------------------- *
CHEKPRNT DS    0H
         OPEN  (SYSPRINT,OUTPUT)
         OI    OPENDFLG,X'01'
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE,MESSAGE1
         PUT   SYSPRINT,MESSAGE1
         MVC   PRTLINE,PRTLINE-1
         PUT   SYSPRINT,PRTLINE
CHEKPREN DS    0H
* ----------------------------------------------------------------- *
PARMDRV  DS    0H         CHECK PARMS AND FLAG THEIR
*                         PRESENCE OR ABSENCE.
*            (PERMISSIBLE PARMS ARE CODED IN PARMTABL.)
         L     R0,=V(PARMCHEK)
         ST    R0,ENTRYPT
         L     R15,ENTRYPT        GET ENTRY POINT OF CALLED PROGRAM
         L     R1,PARMSAVE
         BALR  R14,R15                 DO CALL
         ST    R1,PARMADDR
         USING PARMFLGS,R1              Get bit settings from PARMCHEK
         MVC   PRTLINE,PRTLINE-1        And print the hex values out.
         MVC   PRTLINE+4(16),=C'Parm Settings:  '
         HEX   PRTLINE+27,1,PARMFLG1
         HEX   PRTLINE+30,1,PARMFLG2
         HEX   PRTLINE+33,1,PARMFLG3
         HEX   PRTLINE+36,1,PARMFLG4
         HEX   PRTLINE+39,1,PARMFLG5
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         PUT   SYSPRINT,PRTLINE
         ST    R2,VALUSAVE              Info from PARMCHEK user area
         MVC   PRTLINE,PRTLINE-1        And print it unformatted first.
         MVC   PRTLINE+4(12),=C'User Area : '
         HEX   PRTLINE+16,58,0(R2)
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Length of User Area  :'
         HEX   PRTLINE+30,4,0(R2)
         MVC   PRTLINE+42(6),=C'Hex   '
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Length of last scan  :'
         HEX   PRTLINE+30,4,4(R2)
         MVC   PRTLINE+42(6),=C'Hex   '
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Length of PARM field :'
         HEX   PRTLINE+30,4,8(R2)
         MVC   PRTLINE+42(6),=C'Hex   '
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Address of JCL PARMs :'
         HEX   PRTLINE+30,4,12(R2)
         MVC   PRTLINE+42(6),=C'Hex   '
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Last Character Count :'
         HEX   PRTLINE+30,4,16(R2)
         MVC   PRTLINE+42(6),=C'Hex   '
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Number of PARM Chars :'
         HEX   PRTLINE+30,4,20(R2)
         MVC   PRTLINE+42(6),=C'Hex   '
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Total Chars Searched :'
         HEX   PRTLINE+30,4,24(R2)
         MVC   PRTLINE+42(6),=C'Hex   '
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Parm hits in PARM    :'
         HEX   PRTLINE+30,4,28(R2)
         MVC   PRTLINE+42(6),=C'Packed'
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Parm hits last SYSIN :'
         HEX   PRTLINE+30,4,32(R2)
         MVC   PRTLINE+42(6),=C'Packed'
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Total Parm Hits      :'
         HEX   PRTLINE+30,4,36(R2)
         MVC   PRTLINE+42(6),=C'Packed'
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Parm loops in PARM   :'
         HEX   PRTLINE+30,4,40(R2)
         MVC   PRTLINE+42(6),=C'Packed'
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Parm loops last SYSIN:'
         HEX   PRTLINE+30,4,44(R2)
         MVC   PRTLINE+42(6),=C'Packed'
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Total Parm Loops     :'
         HEX   PRTLINE+30,4,48(R2)
         MVC   PRTLINE+42(6),=C'Packed'
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'Last Table Entry     :'
         HEX   PRTLINE+30,15,52(R2)
         PUT   SYSPRINT,PRTLINE
         MVC   PRTLINE,PRTLINE-1
         MVC   PRTLINE+4(22),=C'User Areas - binary  :'
         HEX   PRTLINE+30,4,80(R2)
         HEX   PRTLINE+40,4,84(R2)
         HEX   PRTLINE+50,4,88(R2)
         HEX   PRTLINE+60,4,92(R2)
         PUT   SYSPRINT,PRTLINE
* ----------------------------------------------------------------- *
* ----------------------------------------------------------------- *
         CLOSE (SYSPRINT)
THEEND   DS    0H
         L     R13,4(,R13)
         MVC   16(4,R13),=F'0'          Return code 0     in R15
         LM    R14,R12,12(R13)
         BR    R14
* -----------------------------------------------------------
*        S U B R O U T I N E S
* -----------------------------------------------------------
* --------------------------------------------------------- *
* --------------------------------------------------------- *
PACK     PACK  WORKA,0(0,1)          <<< EXECUTED >>>
PACKL    PACK  WORKL,0(0,1)          <<< EXECUTED >>>
MVLIMNUM MVC   LIMFMSG+11(0),0(R1)   <<< EXECUTED >>>
MVLBLNUM MVC   LIMLMSG+11(0),0(R1)   <<< EXECUTED >>>
         EJECT
* --------------------------------------------------------- *
VALUWORK DC    CL54' '
VALUSAVE DC    F'0'
WORKA    DC    D'0'
WORKL    DC    D'0'
LINLABEL DC    F'0'           LABEL LIMIT VALUE
PL4NO3   DC    PL4'3'
FF       EQU   X'FF'
LIMFMSG  DC    C' FILELIMIT=           HAS BEEN CODED IN SYSIN. NUMBER'
         DC    C' IS TREATED AS NL.                                   '
         DC    CL27' '
LIMLMSG  DC    C' LABELIMIT=           HAS BEEN CODED IN SYSIN. NUMBER'
         DC    C' REFLECTS THE COUNT OF ----ENDOFLABELTRAILER CARDS.  '
         DC    CL27' '
* --------------------------------------------------------- *
         DS    0F       ALIGN ON FULLWORD
* --------------------------------------------------------- *
         SPACE 3
         LTORG
         SPACE 3
         DS    0D
* --------------------------------------------------------- *
MESSAGE1 DC    CL133'1PARMLINK - DRIVER FOR THE PARMCHEK PROGRAM '
* --------------------------------------------------------- *
         SPACE 3
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
* --------------------------------------------------------- *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
HEX      DS    0H
         MVC   1(1,R15),0(R1)        Move byte
         UNPK  0(3,R15),1(2,R15)     Unpack
         TR    0(2,R15),HEXTAB-240
         LA    R15,2(,R15)           Increment output pointer
         LA    R1,1(,R1)             Increment input pointer
         BCT   R0,HEX                Decrement length, then loop
         MVI   0(R15),C' '           Blank the trailing byte
         BR    R4                    Return to caller
HEXTAB   DC    C'0123456789ABCDEF'   Translate table
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
HEXSAVE  DS    3F                    Register save for HEX macro
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
* --------------------------------------------------------- *
FLAGSYST DC    X'00'          FLAG INDICATING DCB IS PRESENT
*              X'01'          SYSPRINT DD IS PRESENT
*              X'02'          SYSIN    DD IS PRESENT
OPENDFLG DC    X'00'          FLAG INDICATING DCB IS OPEN
*              X'01'          SYSPRINT DD IS OPEN
*              X'02'          SYSIN    DD IS OPEN
* --------------------------------------------------------- *
         SPACE 3
ENTRYPT  DC    F'0'
PARMSAVE DC    F'0'
PARMADDR DC    F'0'
SAV4HEX  DC    F'0'
SAVE2TO3 DC    2F'0'
SAVE3REG DC    F'0'
SAVE7BA1 DC    F'0'
SAVE7REG DC    F'0'
SAVE7BAL DC    F'0'
SAVE7RG1 DC    F'0'
SAVE7RG2 DC    F'0'
SAVE7RG3 DC    F'0'
SAVE7RG4 DC    F'0'
         DC    CL4' '
*    ------------------------ >   Constants for Date and Time - below
SAVE1402 DS    6F                 Extra fullword for safety
TIMED    DS    CL7
DATED    DS    CL9
DATETIME DS    4F
         ORG   DATETIME
TIME     DS    CL4
         DS    CL4
DATE     DS    CL5
         DS    CL3
HEADDLIT DS    0CL31              Literal for headings - date and time
         DC    CL6'Today:'
         DC    CL2' '
HEADDATE DC    CL23' '
LEVELLIT DS    0CL21              Level number display for headings
         DC    CL6'Level '
LEVELL   DC    CL4'&LLEV'
         DC    CL2' ('
LEVDATE  DC    CL8' '             Assembly date for this program
         DC    CL1')'
*    ------------------------ >   Constants for Date and Time - above
*    --------------- >            Output line buffers - below
         DC    C' '
LINE     DC    CL133' '
         DC    C' '
CNTLINE  DC    CL133' '
         DC    C' '
OUTLINE  DC    CL133' '
         DC    C' '
PRTLINE  DC    CL133' '
         DC    CL80' '            Padding
*    --------------- >            Output line buffers - above
*
CARDIN   DC    CL80' '            SYSIN CARD INPUT AREA
* --------------------------------------------------------- *
PARMFLGS DSECT
PARMFLG1 DS    X
PARMFLG2 DS    X
PARMFLG3 DS    X
PARMFLG4 DS    X
PARMFLG5 DS    X
* --------------------------------------------------------- *
         END
