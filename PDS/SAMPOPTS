*          DATA SET SAMPOPTS   AT LEVEL 049 AS OF 05/29/02               00001
   TITLE 'SAMPOPTS -- TAPE COPY PROGRAM OPTIONS DISPLAY'                 00002
**************************************************************           00003
**************************************************************           00004
**                                                          **           00005
**                                                          **           00006
**  MODULE: SAMPOPTS                                        **           00007
**                                                          **           00008
**                                                          **           00009
**  PURPOSE:  SUBROUTINE TO DISPLAY EXECUTION OPTIONS THAT  **           00010
**            HAVE BEEN SET FOR A RUN OF THE PARMCHEK       **           00011
**            PROGRAM.                                      **           00012
**                                                          **           00013
**                                                          **           00014
**************************************************************           00026
**************************************************************           00026
         EJECT                                                           00325
         YREGS                                                           00326
SAMPOPTS CSECT                                                           00327
         USING *,R15                                          SBG 03/00  00328
*  REG 1 IS POINTING TO THE PARM VALUES SET BY THE PARMCHEK PROGRAM.
         B     EYECATCH                                       SBG 03/00  00329
         DC    C'--- '            MAKE EYECATCHER VISIBLE     SBG 03/00  00330
         DC    C'SAMPOPTS - DISPLAY OPTIONS FOR THE PARMCHEK PROGRAM '   00331
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '         SBG 03/00  00332
SAVEAREA DC    18F'0'                                                    00334
EYECATCH DS    0D             ENSURE ALIGNMENT                SBG 03/00  00335
         SAVE  (14,12)                                                   00336
         LR    R5,R1          SAVE PASSED QUANTITY                       00337
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR                   00338
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR          00339
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR         00340
         LR    R13,R1               NEW SAVE AREA ADDRESS                00341
         DROP  R15                                                       00342
* -----                       SET UP TWO BASE REGISTERS                  00343
         USING SAMPOPTS,R12,R11                                          00344
         LR    R12,R15                                                   00345
         LA    R11,4095(,R12)                                            00346
         LA    R11,1(,R11)                                               00347
         LR    R1,R5          RESTORE PARM POINTER                       00354
OPTMOVE  DS    0H
         MVC   PARMTBIN(5),0(R1)      MOVE PARMS INTO THIS PGM
         SR    R1,R1                  DON'T POINT R1 INTO LAST PGM
         SR    R5,R5                  DON'T POINT R5 INTO LAST PGM
         OPEN  (SYSPRINT,OUTPUT)                                         00586
         MVC   LINE(133),LINE-1       CLEAR LINE
         MVC   LINE(133),DISPLINH
         PUT   SYSPRINT,LINE                                             00367
         MVC   LINE(133),LINE-1       CLEAR LINE
         MVC   LINE(133),DISPLINO
         PUT   SYSPRINT,LINE                                             00367
         MVC   LINE(133),LINE-1       CLEAR LINE
         PUT   SYSPRINT,LINE                                             00367
         BAL   R7,FILLOPTS
         MVC   LINE(133),LINE-1       CLEAR LINE
         MVC   LINE(133),DISPOP02
         PUT   SYSPRINT,LINE                                             00367
         MVC   LINE(133),LINE-1       CLEAR LINE
         MVC   LINE(133),DISPOP01
         PUT   SYSPRINT,LINE                                             00367
         MVC   LINE(133),LINE-1       CLEAR LINE
         MVC   LINE(133),DISPOP03
         PUT   SYSPRINT,LINE                                             00367
         CLOSE (SYSPRINT)
* ----------------------------------------------------------------- *
THEEND   EQU   *
THEEND1  DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=0
         EJECT
* - - - - - - - -   SUBROUTINES   - - - - - - - - - - - - - - - - - *
* ----------------------------------------------------------------- *
*    BAL REGISTER IS R7.                                            *
* ------------------------------------------------------------------*
FILLOPTS DS    0H         FILL OPTIONS DISPLAY AREA AND PRINT
         LA    R3,OPTABLE             POINT TO BEGINNING OF TABLE
         LA    R6,DISPLIN1            POINT TO FIRST DISPLAY LINE
         LA    R2,5(,R6)              POINT TO FIRST TARGET LOCATION
FILLBCT  LA    R4,8                   LOAD BCT QUANTITY
FILLOOP  CLC   =X'FFFFFFFF',0(R3)
         BE    FILOPEND
         MVC   PARMWORK(5),0(R3)      MOVE BYTES TO BE TESTED
         NC    PARMWORK(5),PARMTBIN   CHECK IF BYTE IS ON
         BZ    FILLZER
         MVC   0(10,R2),5(R3)         MOVE OPTION TO DISPLAY - BIT ON
         LA    R3,OPTENTLN(,R3)       BUMP TO NEXT TABLE ENTRY
         LA    R2,12(,R2)             BUMP TO NEXT DISPLAY AREA
         BCT   R4,FILLOOP             LOOP BACK
         B     FILNEXTB
FILLZER  DS    0H
         MVC   0(10,R2),15(R3)        MOVE OPTION TO DISPLAY - BIT OFF
         LA    R3,OPTENTLN(,R3)       BUMP TO NEXT TABLE ENTRY
         LA    R2,12(,R2)             BUMP TO NEXT DISPLAY AREA
         BCT   R4,FILLOOP             LOOP BACK
FILNEXTB DS    0H
         MVC   LINE(133),LINE-1       CLEAR OUTPUT AREA
         MVC   LINE(133),0(R6)        MOVE OPTION DISPLAY INTO LINE
         PUT   SYSPRINT,LINE
         LA    R6,133(,R6)            POINT TO BEGINNING OF NEXT LINE
         LA    R2,37(,R2)             BUMP DISPLAY AREA TO NEXT LINE
         B     FILLBCT                RELOAD BCT QUANTITY AND RELOOP
FILOPEND BR    R7
* ----------------------------------------------------------------- *
* ----------  CONSTANTS  ------------------------------------------ *
         LTORG
         DS    0F
PARMTBIN DC    X'0000000000'
PARMWORK DC    X'0000000000'
PARMZERO DC    X'0000000000'
OPTABLE  DS    0D
         DC    X'2000000000',C'LTMSKIP   ',C'LTMCOPY   '
OPTENTLN EQU   *-OPTABLE
         DC    X'0100000000',C'BLKCNT    ',C'NOBLKCNT  '
         DC    X'0200000000',C'HDR1      ',C'NOHDR1    '
         DC    X'0400000000',C'HDR2      ',C'NOHDR2    '
         DC    X'0800000000',C'EOF1      ',C'NOEOF1    '
         DC    X'1000000000',C'EOF2      ',C'NOEOF2    '
         DC    X'4000000000',C'EOV1      ',C'NOEOV1    '
         DC    X'8000000000',C'EOV2      ',C'NOEOV2    '
         DC    X'0010000000',C'LABLDUMP  ',C'NOLABLDU  '
         DC    X'0020000000',C'SYSIN     ',C'NOSYSIN   '
         DC    X'0040000000',C'OUTVOL ***',C'NOOUTVOL  '
         DC    X'0080000000',C'LABADDIN  ',C'NOLABADD  '
         DC    X'0001000000',C'CHGVOL    ',C'NOCHGVOL  '
         DC    X'0002000000',C'VOLLBL    ',C'NOVOLLBL  '
         DC    X'0004000000',C'EOVCHG    ',C'NOEOVCHG  '
         DC    X'0008000000',C'READONLY  ',C'WRITE     '
         DC    X'0000100000',C'PRADDLBL  ',C'NOPRADDL  '
         DC    X'0000200000',C'CORRBLKS  ',C'NOCORRBL  '
         DC    X'0000400000',C'NOEXNULL  ',C'EXNULL    '
         DC    X'0000800000',C'LBLFIX    ',C'NOLBLFIX  '
         DC    X'0000010000',C'INITVOLS  ',C'          '
         DC    X'0000020000',C'LIMFILE **',C'NOLIMFIL  '
         DC    X'0000040000',C'BYTES     ',C'NOBYTES   '
         DC    X'0000080000',C'CUMTOT    ',C'NOCUMTOT  '
         DC    X'0000001000',C'LIMLABEL *',C'NOLIMLAB  '
         DC    X'0000002000',C'OPTION    ',C'NOOPTION  '
         DC    X'0000004000',C'SUPPWTO   ',C'NOSUPPWT  '
         DC    X'0000008000',C'LBDQUICK  ',C'NOLBDQUI  '
         DC    X'0000000100',C'CUMSEP    ',C'NOCUMSEP  '
         DC    X'0000000200',C'          ',C'          '
         DC    X'0000000400',C'          ',C'          '
         DC    X'0000000800',C'          ',C'          '
         DC    X'0000000010',C'          ',C'          '
         DC    X'0000000020',C'          ',C'          '
         DC    X'0000000040',C'          ',C'          '
         DC    X'0000000080',C'          ',C'          '
         DC    X'0000000001',C'          ',C'          '
         DC    X'0000000002',C'          ',C'          '
         DC    X'0000000004',C'          ',C'          '
         DC    X'0000000008',C'          ',C'          '
         DC    X'FFFFFFFFFF'
*  -----
DISPAREA DS    0D
DISPLINH DS    0CL133
         DC    C'1    PARMCHEK PROGRAM - EXTENDED PARM CHEK PROGRAM - '
         DC    C'LEVEL 049 - OPTIONS DISPLAY                          '
         DC    27C' '
DISPLINO DS    0CL133
         DC    C'-    OPTIONS IN EFFECT:                              '
         DC    C'                                                     '
         DC    27C' '
DISPOP01 DS    0CL133
         DC    C'      **  THE LIMFILE OPTION IS ONLY SET BY THE  FILE'
         DC    C'LIMIT=   SYSIN CARD                                  '
         DC    27C' '
DISPOP02 DS    0CL133
         DC    C'      *   THE LIMLABEL OPTION IS ONLY SET BY THE LABE'
         DC    C'LIMIT=   SYSIN CARD                                  '
         DC    27C' '
DISPOP03 DS    0CL133
         DC    C'      *** THE OUTVOL OPTION IS ONLY SET BY THE   TAPE'
         DC    C'OWNER=   OR OUTVOLALL=   SYSIN CARDS                 '
         DC    27C' '
SYSPRINT DCB   MACRF=PM,DDNAME=SYSPRINT,DSORG=PS,RECFM=FA,LRECL=133,   X 02499
               BLKSIZE=133                                               02500
         DC    C' '
LINE     DS    CL133
DISPLIN1 DS    0CL133
         DC    C'0    '
DISPL1A  DC    8CL12' '
         DC    32C' '
DISPLIN2 DS    0CL133
         DC    C'     '
DISPL2A  DC    8CL12' '
         DC    32C' '
DISPLIN3 DS    0CL133
         DC    C'     '
DISPL3A  DC    8CL12' '
         DC    32C' '
DISPLIN4 DS    0CL133
         DC    C'     '
DISPL4A  DC    8CL12' '
         DC    32C' '
DISPLIN5 DS    0CL133
         DC    C'     '
DISPL5A  DC    8CL12' '
         DC    32C' '
         SPACE 3                                                         01722
         EJECT                                                           01722
@PAN$AUD CSECT                                                           02519
$PAN#AUD DC    CL30'LEVEL 049 SAMPOPTS  05/29/02 '                       02520
         END                                                             02521
